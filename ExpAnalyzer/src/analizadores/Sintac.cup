package analizadores; 
import java_cup.runtime.*;
import java.util.LinkedList;

parser code 
{:
     public String resultado = "";
        public static LinkedList<Object> instrucciones;
        public static LinkedList<Object> lexemas;
        public static LinkedList<MErrores> error = new LinkedList<>();
        public void syntax_error(Symbol s)
        {
           error.add(new MErrores("sintactico","Identificador "+s.value + " no reconocido",s.right,s.left));
        }

        public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception
        {
         error.add(new MErrores("sintactico","Identificador "+s.value + " no reconocido",s.right,s.left));
        }
:} 

action code
{:  :}

terminal String LLAVEIZ,LLAVEDER,CONJ,FLECHA;
terminal String SEPARADOR,DISYUNCION,COMA,COLOCHO;
terminal String CONCATENACION,DOSPUNTOS,MAS,POR;
terminal String PUNTOYCOMA,INTERROGACION;
terminal String ID,CADENA,DIGITO;
terminal String LETRA,SIMBOLO;

non terminal INIT;
non terminal LinkedList<Object> instrucciones,lexemas;
non terminal instruc,unitario;
non terminal defconj, rangocolocho,elementolista;
non terminal LinkedList<Interfaces.expresiones.primitivo> lista;
non terminal Interfaces.expresiones.Nodob expregular;
non terminal lexema;

start with INIT; 

INIT::= LLAVEIZ instrucciones:a  SEPARADOR SEPARADOR lexemas:b LLAVEDER {: instrucciones = a; lexemas = b; :}
;

instrucciones ::= instrucciones:a instruc:b {:RESULT = a; RESULT.add(b);:}
                | instruc:a                 {: RESULT = new LinkedList<>(); RESULT.add(a); :}
;

instruc ::= defconj:a PUNTOYCOMA {:RESULT = a;:}
        | ID:a FLECHA  expregular:b PUNTOYCOMA{:RESULT =new Interfaces.expresiones.ExpRegular(aleft,aright,b,a);Interfaces.expresiones.Nodob.contador_hojas=1;:}
; 

defconj::=CONJ DOSPUNTOS ID:a FLECHA unitario:b COLOCHO unitario:c {:RESULT = new Interfaces.expresiones.conj_rango(aleft,aright,a,b,c);:}
        | CONJ DOSPUNTOS ID:a FLECHA lista:b {:RESULT = new Interfaces.expresiones.conj_lista(aleft,aright,b,a);:}
;

lista::= lista:a COMA unitario:b {:RESULT = a; RESULT.add((Interfaces.expresiones.primitivo)b);:}
        | unitario:a {: RESULT = new LinkedList<>(); RESULT.add((Interfaces.expresiones.primitivo)a); :}
;

unitario ::= LETRA:a {:RESULT = new Interfaces.expresiones.primitivo(aleft,aright,a);:}
            |DIGITO:a {:RESULT = new Interfaces.expresiones.primitivo(aleft,aright,a);:}
            |SIMBOLO:a {:RESULT = new Interfaces.expresiones.primitivo(aleft,aright,a);:}
;

expregular ::= CONCATENACION expregular:a expregular:b {:RESULT = new Interfaces.expresiones.Nodob(a,b,".","simbolo");:}
        | DISYUNCION  expregular:a expregular:b {:RESULT = new Interfaces.expresiones.Nodob(a,b,"|","simbolo");:}
        | MAS expregular:a                    {:RESULT =new Interfaces.expresiones.Nodob(a,null,"+","simbolo");:}
        | POR expregular:a                   {:RESULT =new Interfaces.expresiones.Nodob(a,null,"*","simbolo");:}
        | INTERROGACION  expregular:a         {:RESULT =new Interfaces.expresiones.Nodob(a,null,"?","simbolo");:}
        | LLAVEIZ ID:a LLAVEDER               {:RESULT =new Interfaces.expresiones.Nodob(null,null,"{"+a+"}","conjunto");:}
        | CADENA:a                            {:RESULT =new Interfaces.expresiones.Nodob(null,null,a,"cadena");:}
;

lexemas ::= lexemas:a lexema:b {:RESULT = a; RESULT.add(b);:}
          | lexema:a         {: RESULT = new LinkedList<>(); RESULT.add(a); :}
;

lexema ::= ID:a DOSPUNTOS CADENA:b PUNTOYCOMA {: RESULT = new Interfaces.expresiones.lexemas(aleft,aright,a,b);:}
;
